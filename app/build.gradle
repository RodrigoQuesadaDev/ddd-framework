buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.rodrigodev.datanucleus-android'
apply plugin: 'com.rodrigodev.jbehave'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.aticosoft.appointments.mobile.business"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.main.java.srcDirs += ['src/domain/java']
    sourceSets.test {
        java.srcDirs = ['src/domainTest/java', 'src/domainSpecs/java']
        resources.srcDirs += ['src/domainSpecs/resources']
    }

    packagingOptions {
        exclude 'META-INF/README.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'plugin.xml'
    }
}

kapt {
    generateStubs = true
}

datanucleus {
    entitiesDirs = ['com/aticosoft/appointments/mobile/business/domain/model', 'com/aticosoft/appointments/mobile/business/domain/testing/model']
    alwaysDetachable true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile(project(':common')) {
        transitive = false
    }
    compile "com.google.guava:guava:$guavaVersion"
    compile "joda-time:joda-time:$jodaTimeVersion"
    compile "com.googlecode.libphonenumber:libphonenumber:$libphonenumberVersion"
    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compile "javax.annotation:jsr250-api:$jsr250ApiVersion" // needed for Dagger
    compile "commons-io:commons-io:$commonsIoVersion"
    //datanucleus "org.datanucleus:datanucleus-accessplatform-jdo-rdbms:$datanucleusJdoRdbmsVersion"
    datanucleus "org.datanucleus:datanucleus-core:$datanucleusCoreVersion"
    datanucleus "org.datanucleus:datanucleus-api-jdo:$datanucleusApiJdoVersion"
    datanucleus "org.datanucleus:datanucleus-rdbms:$datanucleusRdbmsVersion"
    datanucleus "org.datanucleus:datanucleus-jodatime:$datanucleusJodatimeVersion"
    datanucleus "javax.jdo:jdo-api:$jdoApiVersion"
    compile("com.querydsl:querydsl-jdo:$queryDslVersion") {
        exclude module: 'jdo-api'
    }
    kapt "com.querydsl:querydsl-apt:$queryDslVersion:jdo"
    kapt "javax.jdo:jdo-api:$jdoApiVersion"
    jdkClasses files('jdk-files/harmony-javax-jndi.jar')
    jarjar files("$buildSystemPath/jarjar/jarjar-1.4.jar")
    compile "com.h2database:h2:$h2Version"
    compile "io.reactivex:rxjava:$rxJavaVersion"
    compile "io.reactivex:rxkotlin:$rxKotlinVersion"
    compile "io.reactivex:rxandroid:$rxAndroidVersion"
    compile "io.reactivex:rxjava-math:$rxJavaMathVersion"
    compile "io.reactivex:rxjava-string:$rxJavaStringVersion"
    compile "io.reactivex:rxjava-computation-expressions:$rxJavaComputationExpressionsVersion"
    compile "io.reactivex:rxjava-joins:$rxJavaJoinsVersion"
    compile "io.reactivex:rxjava-async-util:$rxJavaAsyncVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    testCompile project(':testingCommon')
    testCompile "org.jbehave:jbehave-core:$jBehaveVersion"
    jbehaveSiteResources "org.jbehave:jbehave-core:$jBehaveVersion"
    jbehaveSiteResources "org.jbehave.site:jbehave-site-resources:$jBehaveSiteVersion:@zip"
    testCompile "junit:junit:$junitVersion"
    testCompile "org.assertj:assertj-core:$assertjVersion"
    testCompile "org.robolectric:robolectric:$robolectricVersion"
    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"
    //TODO remove/change line below when this issue is fixed? https://bitbucket.org/hvisser/android-apt/issues/22/dagger-robolectric-gradle-apt-source-not
    //testApt "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptTest "com.querydsl:querydsl-apt:$queryDslVersion:jdo"
    kaptTest "javax.jdo:jdo-api:$jdoApiVersion"
}

//TODO remove/change line below when this issue is fixed? https://bitbucket.org/hvisser/android-apt/issues/22/dagger-robolectric-gradle-apt-source-not
//apply from: "$buildSystemPath/testing/add-generated-test-sources.gradle"